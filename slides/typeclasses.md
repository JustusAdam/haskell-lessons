---
title: Typeclasses
author: Justus Adam
date: 27.04.2017
---

# What is it

---

- define a common behavior for a set of types
- very similar to interfaces
- more similar still to rust traits and swift protocols

# Defininig classes

---

- keyword for definition is `class`
- class names are upper case
- each class definition is done for a type variable

---

Example: `Ord` implements ordering

```hs
class Ord a where
```

## Member functions

The body contains names and signatures for member functions

```hs
class Ord a where

    compare :: a -> a -> Ordering
    (<=) :: a -> a -> Bool
    max :: a -> a -> a
```

## Superclasses

If you want to implement `Ord` you must also implement `Eq`.

```hs
class Eq a => Ord a where

    compare :: a -> a -> Ordering
    (<=) :: a -> a -> Bool
    max :: a -> a -> a
```

## Default implementations

```hs
class Eq a => Ord a where

    compare :: a -> a -> Ordering
    compare x y = if x == y then EQ
                else if x <= y then LT
                else GT

    (<=) :: a -> a -> Bool
    x <= y = case compare x y of { GT -> False; _ -> True }

    max :: a -> a -> a
    max x y = if x <= y then y else x
```

<aside class="notes">
- Show them the docs for `Ord` and point out the `MINIMAL` definition
</aside>

# Constraining types

---

- To use a class add a constraint to the signature
- Constraints always apply to type variables
- Multiple constraints per signature and type

```hs
max3 :: Ord a => a -> a -> a -> a
max3 a1 a2 a3 = a1 `max` a2 `max` a3

showMax3 :: (Show a, Ord a) => a -> a -> a -> String
showMax3 a1 a2 a3 = show (max3 a1 a2 a3)
```

# Implementing classes

---

- Can be done anywhere
- no other instance can be in scope
- typically done either with class or type definition
- use the `instance` keyword
- looks similar to class definition

---

```hs
data MyType = TheSmallest | TheMiddle | TheLargest

instance Eq MyType where

instance Ord MyType where
```

---

Body is only implementations, no signatures.

```hs
data MyType = TheSmallest | TheMiddle | TheLargest

instance Eq MyType where
    TheSmallest == TheSmallest = True
    TheMiddle   == TheMiddle   = True
    TheLargest  == TheLargest  = True
    _           == _           = False

instance Ord MyType where

    compare TheSmallest TheSmallest = EQ -- EQual
    compare TheLargest TheLargest = EQ 
    compare TheMiddle TheMiddle = EQ
    compare TheSmallest _ = LT -- Less Then
    compare TheLargest  _ = GT -- Greater Then
    compare _ TheLargest = LT
    compare _ TheSmallest = GT

    TheSmallest <= _ = True
    _ <= TheLargest = True
    TheLargest <= _ = False
    _ <= TheSmallest = False

```

## Deriving classes

- `Eq`, `Ord`, `Show` and `Read` can be generated by the compiler
- See the reference pages in how these instances work

```hs
data T = A | B Int deriving (Show, Eq, Ord)
```
